import nu.studer.gradle.jooq.JooqEdition

plugins {
    id 'idea'
    id 'java-library'
    id 'maven-publish'
    id 'com.github.johnrengelman.shadow'
    id 'nu.studer.jooq' version '5.2.1'
}

sourceSets {
    ap {
        compileClasspath += main.compileClasspath + main.output
    }
}

jar {
    from sourceSets.ap.output
}

shadowJar.classifier = ''

dependencies {
    // Database Drivers
    jooqGenerator 'org.postgresql:postgresql:42.5.1'
    api 'org.postgresql:postgresql:42.5.1'
    api 'org.jooq:jooq:3.17.3'
    api group: 'com.velocitypowered', name: 'velocity-utils-api', version: '4.0.0-SNAPSHOT', changing: true

    // Lombok
    compileOnly group: 'org.projectlombok', name: 'lombok', version: lombokVersion
    annotationProcessor group: 'org.projectlombok', name: 'lombok', version: lombokVersion

    // Logging API Dependencies
    api group: 'org.slf4j', name: 'slf4j-api', version: slf4jVersion

    // Discord API Dependencies
    api(group: 'net.dv8tion', name: 'JDA', version: jdaVersion, changing: true) {
        exclude module: 'opus-java'
    }

    // Command API Dependencies
    api group: 'co.aikar', name: 'acf-core', version: '0.5.0-SNAPSHOT', changing: true
    // Config API Dependencies
    api 'com.electronwill.night-config:toml:3.6.6'
    api 'com.electronwill.night-config:json:3.6.6'

    // Utilities
    api 'com.google.code.gson:gson:2.10.1'
    api "com.google.guava:guava:${guavaVersion}"
    api 'com.google.inject:guice:7.0.0'
}

jooq {
    version = '3.16.4'
    edition = JooqEdition.OSS
    configurations {
        main {
            generateSchemaSourceOnCompilation = false
            generationTool {
                logging = org.jooq.meta.jaxb.Logging.INFO
                jdbc {
                    driver = 'org.postgresql.Driver'
                    url = postgresUrl
                    user = postgresUser
                    password = postgresPass
                }
                generator {
                    name = 'org.jooq.codegen.DefaultGenerator'
                    database {
                        name = 'org.jooq.meta.postgres.PostgresDatabase'
                        includes = 'guilds'
                        excludes = ''
                        inputSchema = 'public'
                    }
                    target {
                        packageName = 'dev.armadeus.bot.database'
                        directory = 'src/database/java'  // default (can be omitted)
                    }
                }
            }
        }
    }
}

publishing {
    repositories {
        repositories {
            maven {
                credentials {
                    username "$nexusUser"
                    password "$nexusPass"
                }
                if (!project.version.endsWith('-SNAPSHOT')) {
                    url "https://mvn.armadeus.dev/repository/maven-releases"
                } else {
                    url "https://mvn.armadeus.dev/repository/maven-snapshots"
                }
            }
        }
    }
    publications {
        maven(MavenPublication) {
            groupId = rootProject.group
            artifactId = 'arma-api'
            version = version
            artifact shadowJar
            artifact sourcesJar
        }
    }
}

